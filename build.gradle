plugins {
    id 'java'
    id 'com.diffplug.spotless' version '6.23.3'
}

group = 'by.bsuir'
version = '1.0-SNAPSHOT'

allprojects {
    repositories {
        mavenCentral()
    }

    apply plugin: 'com.diffplug.spotless'

    spotless {
        java {
            target 'src*.java'
            googleJavaFormat('1.17.0').aosp().reflowLongStrings()
            removeUnusedImports()
            trimTrailingWhitespace()
            endWithNewline()
            indentWithSpaces(4)
            formatAnnotations()
        }
    }
}

subprojects {
    apply plugin: 'java'
    apply plugin: 'checkstyle'
    apply plugin: 'jacoco'

    java {
        toolchain {
            languageVersion = JavaLanguageVersion.of(21)
        }
    }

    tasks.withType(JavaCompile).configureEach {
        options.release = 21
        options.encoding = 'UTF-8'
        options.compilerArgs += ['-Xlint:all', '-Xlint:-serial']
    }

    checkstyle {
        toolVersion = '10.12.5'
        configFile = rootProject.file('config/checkstyle/checkstyle.xml')
        ignoreFailures = true
        showViolations = true
    }

    tasks.withType(Checkstyle) {
        reports {
            xml.required = false
            html.required = true
        }
    }

    jacoco {
        toolVersion = '0.8.11'
    }

    test {
        useJUnitPlatform()
        finalizedBy jacocoTestReport
    }

    jacocoTestReport {
        dependsOn test
        reports {
            xml.required = true
            html.required = true
            csv.required = false
        }
    }

    jacocoTestCoverageVerification {
        violationRules {
            rule {
                limit {
                    minimum = 0.50
                }
            }
        }
    }
}
